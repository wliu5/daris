/**
 * JavaCC template file created by SF JavaCC plugin 1.5.28+ wizard for JavaCC 1.5.0+
 */

options
{
  STATIC = false;
}

PARSER_BEGIN(AssetPathCompiler)
package daris.plugin.asset.path;
import java.io.ByteArrayInputStream;
import arc.mf.plugin.ServiceExecutor;
import arc.xml.XmlDoc;

public class AssetPathCompiler
{
  private ServiceExecutor _executor;

  private XmlDoc.Element _ae;

  public AssetPathCompiler(ServiceExecutor executor, XmlDoc.Element ae, String s)
  {
    this (new ByteArrayInputStream(s.getBytes()));
    _executor = executor;
    _ae = ae;
  }

  public static void main(String args []) throws ParseException
  {
    AssetPathCompiler parser = new AssetPathCompiler(System.in);
    while (true)
    {
      System.out.println(parser.parse());
    }
  }
}

PARSER_END(AssetPathCompiler)

SKIP : /* WHITE SPACE */
{
  " "
| "\t"
| "\r"
| "\f"
}

TOKEN :
{
  < EOL : "\n" >
}

TOKEN : /* RESERVED WORDS AND LITERALS */
{
  < CID : "cid" >
| < XVALUE : "xvalue" >
| < PVALUE : "pvalue" >
| < RVALUE : "rvalue" >
| < REPLACE : "replace" >
| < JOIN : "join" >
| < SUBST : "subst" >
}

TOKEN : /* LITERALS */
{
  < STRING :
    "'"
    (
      (~[ "'", "\"", "\\", "\n", "\r" ])
    |
      (
        "\\"
        (
          [ "n", "t", "b", "r", "f", "\\", "'", "\"" ]
        | [ "0"-"7" ] ([ "0"-"7" ])?
        | [ "0"-"3" ] [ "0"-"7" ] [ "0"-"7" ]
        )
      )
    )*
    "'" 
  >
| < POSITIVE_INTEGER :
    "0"
  | [ "1"-"9" ] ([ "0"-"9" ])* >
}

TOKEN : /* SEPARATORS */
{
  < LPAREN : "(" >
| < RPAREN : ")" >
| < COMMA : "," >
| < SLASH : "/" >
}

TOKEN : /* OPERATORS */
{
  < OR : "|" >
}

String parse() :
{
  String r;
}
{
  r = path()
  (
    < EOL >
  | < EOF >
  )
  {
    return r;
  }
}

String path() :
{
  StringBuilder sb = new StringBuilder();
  String v;
}
{
  (
    < SLASH >
    {
      sb.append("/");
    }
  )?
  v = expression()
  {
    sb.append(v);
  }
  (
    < SLASH > v = expression()
    {
      sb.append("/").append(v);
    }
  )*
  {
    return sb.toString();
  }
}

String expression() :
{
  String s;
}
{
  (
    s = cid()
  | s = xvalue()
  | s = pvalue()
  | s = rvalue()
  | s = svalue()
  | s = replace()
  | s = join()
  | s = subst()
  )
  {
    return s;
  }
}

String cid() :
{
  int idx1;
  int idx2;
  StringBuilder sb = new StringBuilder();
}
{
  < CID > < LPAREN > idx1 = integer() 
  < COMMA > idx2 = integer() 
  < RPAREN >
  {
    sb.append("cid(").append(idx1).append(",").append(idx2).append(")");
    return sb.toString();
  }
}

int integer() :
{
  Token t;
  int i;
  boolean negtive = false;
}
{
  ("-"
  {
    negtive = true;
  }
  )? 
  t = < POSITIVE_INTEGER >
  {
    i = Integer.parseInt(t.image);
  }
  {
    if (negtive)
    {
      i = i * (- 1);
    }
    return i;
  }
}

String svalue() :
{
  Token t;
  String s;
}
{
  t = < STRING >
  {
    s = t.image;
    return s.substring(1, s.length() - 1);
  }
}

String xvalue() :
{
  String xpath;
  String defaultValue = null;
}
{
  < XVALUE > < LPAREN > xpath = svalue()
  (
    < COMMA > defaultValue = expression()
  )?
  < RPAREN >
  {
    StringBuilder sb = new StringBuilder("xvalue(");
    sb.append(xpath);
    if (defaultValue != null)
    {
      sb.append(",").append(defaultValue);
    }
    sb.append(")");
    return sb.toString();
  }
}

String pvalue() :
{
  Token t;
  int level;
  String xpath;
  String defaultValue = null;
  StringBuilder sb = new StringBuilder();
}
{
  < PVALUE > < LPAREN > t = < POSITIVE_INTEGER >
  {
    level = Integer.parseInt(t.image);
  }
  < COMMA > xpath = svalue()
  (
    < COMMA > defaultValue = expression()
  )?
  < RPAREN >
  {
    sb.append("pvalue(");
    sb.append(level);
    sb.append(",").append(xpath);
    if (defaultValue != null)
    {
      sb.append(",").append(defaultValue);
    }
    sb.append(")");
    return sb.toString();
  }
}

String rvalue() :
{
  String type;
  String xpath;
  String defaultValue = null;
  StringBuilder sb = new StringBuilder();
}
{
  < RVALUE > < LPAREN > type = svalue() < COMMA > xpath = svalue()
  (
    < COMMA > defaultValue = expression()
  )?
  < RPAREN >
  {
    sb.append("pvalue(");
    sb.append(type);
    sb.append(",").append(xpath);
    if (defaultValue != null)
    {
      sb.append(",").append(defaultValue);
    }
    sb.append(")");
    return sb.toString();
  }
}

String replace() :
{
  String s;
  String m;
  String r;
}
{
  < REPLACE > < LPAREN > s = expression() < COMMA > m = svalue() < COMMA > r = svalue() < RPAREN >
  {
    return s.replace(m, r);
  }
}

String join() :
{
  String s;
  StringBuilder sb = new StringBuilder();
}
{
  < JOIN > < LPAREN > s = expression()
  {
    sb.append(s);
  }
  (
    < COMMA > s = expression()
    {
      sb.append(s);
    }
  )*
  < RPAREN >
  {
    return sb.toString();
  }
}

String subst() :
{
  String s;
  StringBuilder sb = new StringBuilder();
}
{
  < SUBST > < LPAREN > s = expression()
  {
    if (sb.length() <= 0)
    {
      sb.append(s);
    }
  }
  (
    < COMMA > s = expression()
    {
      if (sb.length() <= 0)
      {
        sb.append(s);
      }
    }
  )*
  < RPAREN >
  {
    return sb.toString();
  }
}
